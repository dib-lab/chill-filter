GENOMES = ['bosTau9',
           'canFam6',
           'equCab3',
           'felCat9',
           'galGal6',
           'hg38',
           'mm39',
           'oviAri4',
           'susScr11']

rule all:
    input:
        'animals-and-gtdb.rocksdb',

rule index_animals_and_gtdb:
    input:
        "animals-and-gtdb.mf.csv",
    output:
        protected(directory("animals-and-gtdb.rocksdb")),
    shell: """
        sourmash scripts index {input} -o {output} -k 51
    """

rule animals_and_gtdb:
    input:
        expand('outputs/{g}.k51.100k.sig.zip', g=GENOMES),
        'outputs/all-gtdb-rs220.k51.100k.sig.zip',
    output:
        'animals-and-gtdb.mf.csv',
    shell: """
        sourmash sig collect -F csv {input} -o {output} --abspath
    """


rule downsample:
    input:
        "downloads/{g}.sig.zip",
    output:
        "outputs/{g}.k51.100k.sig.zip",
    shell: """
        sourmash sig downsample -k 51 --scaled 100_000 {input} -o {output}
    """


rule merge_microbial:
    input:
        ancient('downloads/gtdb-rs220-k51-s100_000.zip'),
    output:
        protected('outputs/all-gtdb-rs220.k51.100k.sig.zip'),
    shell: """
        sourmash sig merge {input} -o {output} --set-name "bacteria and archaea (GTDB rs220)"
    """
    
